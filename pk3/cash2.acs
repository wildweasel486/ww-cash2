#include "zcommon.acs"
#library "CASH2"

Script "AutoReload"(void)
{
	SetResultValue(GetCVar("autoreload"));
}
SCRIPT "ActivatePDA" (VOID)
{
    ACS_ExecuteAlways(9001,0);
}

str machinedesc[11][2]=
{
	{"SHOPPDA_NAME","SHOPPDA_MAIN"},
	{"SHOP1_NAME","SHOP1_MAIN"},
	{"SHOP2_NAME","SHOP2_MAIN"},
	{"SHOP3_NAME","SHOP3_MAIN"},
	{"SHOP4_NAME","SHOP4_MAIN"},
	{"SHOP5_NAME","SHOP5_MAIN"},
	{"SHOP6_NAME","SHOP6_MAIN"},
	{"SHOP7_NAME","SHOP7_MAIN"},
	{"SHOPHELL_NAME","SHOPHELL_MAIN"},
	{"SHOPAMMO_NAME","SHOPAMMO_MAIN"},
	{"SHOPARMOR_NAME","SHOPARMOR_MAIN"},
};
//Important! Always end the array with a null entry (see below) as a delimiter
str weaponstats[11][9][6]=
{
	//PDA
	{
		//ActorName,UpgradeName,TinyName,Icon,Description,Statistics
		{"ArtiCarePackageMedical","","SHOPPDA_ITEM1NAME","NOICON","SHOPPDA_ITEM1DESC","SHOPPDA_ITEM1STATS"},
		{"ArtiCarePackagePistol","","SHOPPDA_ITEM2NAME","NOICON","SHOPPDA_ITEM2DESC","SHOPPDA_ITEM2STATS"},
		{"ArtiCarePackageShotgun","","SHOPPDA_ITEM3NAME","NOICON","SHOPPDA_ITEM3DESC","SHOPPDA_ITEM3STATS"},
		{"ArtiCarePackageRifle","","SHOPPDA_ITEM4NAME","NOICON","SHOPPDA_ITEM4DESC","SHOPPDA_ITEM4STATS"},
		{"","","","EMPTICON","",""}
	},
	//Machine 1
	{
		//ActorName,UpgradeName,TinyName,Icon,Description,Statistics
		{"Health","","SHOP1_ITEM1NAME","NOICON","SHOP1_ITEM1DESC","SHOP1_ITEM1STATS"},
		{"CHCKnife","","SHOP1_ITEM3NAME","NOICON","SHOP1_ITEM3DESC","SHOP1_ITEM3STATS"},
		{"Crowbar","","SHOP1_ITEM2NAME","NOICON","SHOP1_ITEM2DESC","SHOP1_ITEM2STATS"},
		{"FireAxe","","SHOP1_ITEM4NAME","NOICON","SHOP1_ITEM4DESC","SHOP1_ITEM4STATS"},
		{"","","","EMPTICON","",""}
	},
	//Machine 2
	{
		//ActorName,UpgradeName,TinyName,Icon,Description,Statistics
		{"SinglePistol","PistolUpgrade","SHOP2_ITEM1NAME","NOICON","SHOP2_ITEM1DESC","SHOP2_ITEM1STATS"},
		{"PunchGun","","SHOP2_ITEM2NAME","NOICON","SHOP2_ITEM2DESC","SHOP2_ITEM2STATS"},
		{"KestrelMagnum","","SHOP2_ITEM3NAME","NOICON","SHOP2_ITEM3DESC","SHOP2_ITEM3STATS"},
		{"PistolAmmo","","SHOP_AMMO1LABEL","PSTAICON","SHOP_AMMO1DESC",""},
		{"HeavyPistolAmmo","","SHOP_AMMO6LABEL","MAGAICON","SHOP_AMMO6DESC",""},
		{"ShotgunAmmo","","SHOP_AMMO2LABEL","12GAICON","SHOP_AMMO2DESC",""},
		{"","","","EMPTICON","",""}
	},
	//Machine 3
	{
		//ActorName,UpgradeName,TinyName,Icon,Description,Statistics
		{"PumpShotgun","PumpShotgunUpgrade","SHOP3_ITEM1NAME","NOICON","SHOP3_ITEM1DESC","SHOP3_ITEM1STATS"},
		{"CHCSMG","","SHOP3_ITEM2NAME","SMGICON","SHOP3_ITEM2DESC","SHOP3_ITEM2STATS"},
		{"LeverActionRifle","","SHOP3_ITEM3NAME","NOICON","SHOP3_ITEM3DESC","SHOP3_ITEM3STATS"},
		{"PistolAmmo","","SHOP_AMMO1LABEL","PSTAICON","SHOP_AMMO1DESC",""},
		{"ShotgunAmmo","","SHOP_AMMO2LABEL","12GAICON","SHOP_AMMO2DESC",""},
		{"HeavyPistolAmmo","","SHOP_AMMO6LABEL","MAGAICON","SHOP_AMMO6DESC",""},
		{"","","","EMPTICON","",""}
	},
	//Machine 4
	{
		//ActorName,UpgradeName,TinyName,Icon,Description,Statistics
		{"PugRifle","","SHOP4_ITEM1NAME","ARIFICON","SHOP4_ITEM1DESC","SHOP4_ITEM1STATS"},
		{"BattleRifle","","SHOP4_ITEM3NAME","NOICON","SHOP4_ITEM3DESC","SHOP4_ITEM3STATS"},
		{"HeavyMachineGun","","SHOP4_ITEM4NAME","M60ICON","SHOP4_ITEM4DESC","SHOP4_ITEM4STATS"},
		{"CHCSMG","","SHOP4_ITEM2NAME","SMGICON","SHOP4_ITEM2DESC","SHOP3_ITEM2STATS"},
		{"LightRifleAmmo","","SHOP_AMMO3LABEL","LRFAICON","SHOP_AMMO3DESC",""},
		{"HeavyRifleAmmo","","SHOP_AMMO4LABEL","HRFAICON","SHOP_AMMO4DESC",""},
		{"","","","EMPTICON","",""}
	},
	//Machine 5
	{
		//ActorName,UpgradeName,TinyName,Icon,Description,Statistics
		{"RecoillessRifle","","SHOP5_ITEM1NAME","RCOLICON","SHOP5_ITEM1DESC","SHOP5_ITEM1STATS"},
		{"MGl140","","SHOP5_ITEM3NAME","MGLICON","SHOP5_ITEM3DESC","SHOP5_ITEM3STATS"},
		{"ArtiHandGrenade","","SHOP5_ITEM2NAME","GRNAICON","SHOP5_ITEM2DESC","SHOP5_ITEM2STATS"},
		{"RecoillessRockets","","SHOP_AMMO5LABEL","NOICON","SHOP_AMMO5DESC",""},
		{"40mmAmmo","","SHOP_AMMO7LABEL","40MAICON","SHOP_AMMO7DESC",""},
		{"","","","EMPTICON","",""}
	},
	//Machine 6
	{
		//ActorName,UpgradeName,TinyName,Icon,Description,Statistics
		{"MOBlaster","","SHOP6_ITEM1NAME","PLASDART","SHOP6_ITEM1DESC","SHOP6_ITEM1STATS"},
		{"GaussRifle","","SHOP6_ITEM2NAME","GAUSICON","SHOP6_ITEM2DESC","SHOP6_ITEM2STATS"},
		{"ArtiElectricGrenade","","SHOP6_ITEM6NAME","EGRAICON","SHOP6_ITEM6DESC","SHOP6_ITEM6STATS"},
		{"","","","EMPTICON","",""}
	},
	//Machine 7
	{
		//ActorName,UpgradeName,TinyName,Icon,Description,Statistics
		{"TestSuperWeapon","","SHOP7_ITEM1NAME","","SHOP7_ITEM1DESC","SHOP7_ITEM1STATS"},
		{"","","","EMPTICON","",""}
	},
	//Machine 8 (Hell)
	{
		//ActorName,UpgradeName,TinyName,Icon,Description,Statistics
		{"FistUpgrade","","SHOPHELL_ITEM1NAME","","SHOPHELL_ITEM1DESC",""},
		{"KnifeUpgrade","","SHOPHELL_ITEM2NAME","","SHOPHELL_ITEM2DESC",""},
		{"CrowbarUpgrade","","SHOPHELL_ITEM3NAME","","SHOPHELL_ITEM3DESC",""},
		{"FireAxeUpgrade","","SHOPHELL_ITEM4NAME","","SHOPHELL_ITEM4DESC",""},
		{"StrengthGiver","","SHOPHELL_ITEM5NAME","","SHOPHELL_ITEM5DESC",""},
		{"","","","EMPTICON","",""}
	},
	//Machine 9 (Ammo)
	{
		//ActorName,UpgradeName,TinyName,Icon,Description,Statistics
		{"PistolAmmo","","SHOP_AMMO1LABEL","PSTAICON","SHOP_AMMO1DESC",""},
		{"HeavyPistolAmmo","","SHOP_AMMO6LABEL","MAGAICON","SHOP_AMMO6DESC",""},
		{"ShotgunAmmo","","SHOP_AMMO2LABEL","12GAICON","SHOP_AMMO2DESC",""},
		{"LightRifleAmmo","","SHOP_AMMO3LABEL","LRFAICON","SHOP_AMMO3DESC",""},
		{"HeavyRifleAmmo","","SHOP_AMMO4LABEL","HRFAICON","SHOP_AMMO4DESC",""},
		{"ArtiHandGrenade","","SHOP5_ITEM2NAME","GRNAICON","SHOP5_ITEM2DESC","SHOP5_ITEM2STATS"},
		{"","","","EMPTICON","",""}
	},
	//Machine 10 (Armor)
	{
		//ActorName,UpgradeName,TinyName,Icon,Description,Statistics
		{"CHCBasicArmor","","SHOPARMOR_ITEM1NAME","ARM1ICON","SHOPARMOR_ITEM1DESC","SHOPARMOR_ITEM1STATS"},
		{"CHCAdvancedArmor","","SHOPARMOR_ITEM2NAME","ARM2ICON","SHOPARMOR_ITEM2DESC","SHOPARMOR_ITEM2STATS"},
		{"","","","EMPTICON","",""}
	}
};
int weaponamounts[11][9][8]=
{
	//PDA
	{
		//buy,sell,fix,upgrade,buyamt,sellamt,maxamt,isweapon(bool)
		{400,0,0,0,1,0,0x7fffffff,0},
		{300,0,0,0,1,0,0x7fffffff,0},
		{350,0,0,0,1,0,0x7fffffff,0},
		{500,0,0,0,1,0,0x7fffffff,0},
		{0,0,0,0,0,0,0,0}
	},
	//Machine 1
	{
		//buy,sell,fix,upgrade,buyamt,sellamt,maxamt,isweapon(bool)
		{25,10,0,0,10,5,100,0},
		{50,25,0,0,1,1,1,1},
		{60,30,0,0,1,1,1,1},
		{90,45,0,0,1,1,1,1},
		{0,0,0,0,0,0,0,0}
	},
	//Machine 2
	{
		//buy,sell,fix,upgrade,buyamt,sellamt,maxamt,isweapon(bool)
		{200,100,0,100,1,1,1,1},
		{75,37,0,0,1,1,1,1},
		{300,150,0,0,1,1,1,1},
		{15,0,0,0,30,0,0x7fffffff,0},
		{20,0,0,0,14,0,0x7fffffff,0},
		{25,0,0,0,10,0,0x7fffffff,0},
		{0,0,0,0,0,0,0,0}
	},
	//Machine 3
	{
		//buy,sell,fix,upgrade,buyamt,sellamt,maxamt,isweapon(bool)
		{400,250,0,600,1,1,1,1},
		{350,175,0,0,1,1,1,1},
		{250,125,0,0,1,1,1,1},
		{15,0,0,0,30,0,0x7fffffff,0},
		{25,0,0,0,10,0,0x7fffffff,0},
		{20,0,0,0,14,0,0x7fffffff,0},
		{0,0,0,0,0,0,0,0}
	},
	//Machine 4
	{
		//buy,sell,fix,upgrade,buyamt,sellamt,maxamt,isweapon(bool)
		{500,250,0,0,1,1,1,1},
		{800,400,0,0,1,1,1,1},
		{1600,800,0,0,1,1,1,1},
		{250,125,0,0,1,1,1,1},
		{35,0,0,0,45,0,0x7fffffff,0},
		{50,0,0,0,15,0,0x7fffffff,0},
		{0,0,0,0,0,0,0,0}
	},
	//Machine 5
	{
		//buy,sell,fix,upgrade,buyamt,sellamt,maxamt,isweapon(bool)
		{2000,1000,0,0,1,1,1,1},
		{1200,600,0,0,1,1,1,1},
		{75,0,0,0,1,0,0x7fffffff,0},
		{200,0,0,0,1,0,0x7fffffff,0},
		{65,0,0,0,6,0,0x7fffffff,0},
		{0,0,0,0,0,0,0,0}
	},
	//Machine 6
	{
		//buy,sell,fix,upgrade,buyamt,sellamt,maxamt,isweapon(bool)
		{100,25,0,0,1,1,1,1},
		{2500,1250,0,0,1,1,1,1},
		{130,0,0,0,1,1,0x7fffffff,0},
		{0,0,0,0,0,0,0,0}
	},
	//Machine 7
	{
		//buy,sell,fix,upgrade,buyamt,sellamt,maxamt,isweapon(bool)
		{25000,0,0,0,1,0,1,1},
		{0,0,0,0,0,0,0,0},
	},
	//Machine 8 (Hell)
	{
		//buy,sell,fix,upgrade,buyamt,sellamt,maxamt,isweapon(bool)
		{1000,0,0,65,1,0,1,0},
		{1500,0,0,75,1,0,1,0},
		{2000,0,0,85,1,0,1,0},
		{1750,0,0,80,1,0,1,0},
		{800,0,0,50,1,0,0,0},
		{0,0,0,0,0,0,0,0}
	},
	//Machine 9 (Ammo)
	{
		//buy,sell,fix,upgrade,buyamt,sellamt,maxamt,isweapon(bool)
		{10,0,0,0,30,0,0x7fffffff,0},
		{12,0,0,0,14,0,0x7fffffff,0},
		{15,0,0,0,10,0,0x7fffffff,0},
		{20,0,0,0,45,0,0x7fffffff,0},
		{38,0,0,0,15,0,0x7fffffff,0},
		{60,0,0,0,1,0,0x7fffffff,0},
		{0,0,0,0,0,0,0,0},
	},
	//Machine 10 (Armor)
	{
		//buy,sell,fix,upgrade,buyamt,sellamt,maxamt,isweapon(bool)
		{75,0,0,0,1,0,0,0},
		{150,0,0,0,1,0,0,0},
		{0,0,0,0,0,0,0,0}
	},
};
int productcursorcoords[6]=
{
	10.1,74.1,138.1,202.1,266.1,330.1
};
int actioncursorcoords[4]=
{
	216.1,294.1,372.1,450.1
};

int menuopen[8];
//Shop menu -- Big thanks to Psychic, by Xaser and team
//Making it a numbered script until I can figure out how to assign a named to the actors in DECORATE...
script 9001(int machine)
{
	if(menuopen[PlayerNumber()]||GetActorProperty(0,APROP_Health)<=0)
	{
		CloseMenu();
		terminate;
	}
	PlaySound(0,"shop/open");
	menuopen[PlayerNumber()]=1;
	setplayerproperty(0,1,PROP_TOTALLYFROZEN);
	sethudsize(640,400,0);
	int input,oldinput,movestart,moveloop,movedelay=6,choicedelay=1,listoffset,productcursorpos,
	actioncursorpos,listlength;
	for(int i=0;StrCmp(weaponstats[machine][i][2],"");i++)
		listlength++;
	while(1)
	{
		if(GetActorProperty(0,APROP_Health)<=0)
		{
			CloseMenu();
			terminate;
		}
		input=GetPlayerInput(-1,INPUT_BUTTONS);
		oldinput=GetPlayerInput(-1,INPUT_OLDBUTTONS);
		if(!input && !oldinput)
			choicedelay=0;
		if((!choicedelay&&(input & BT_FORWARD||input & BT_BACK||input & BT_USE||
		input & BT_JUMP||input & BT_ATTACK||input & BT_ZOOM||input & BT_LEFT||
		input & BT_MOVELEFT||input & BT_RIGHT||input & BT_MOVERIGHT))||!menuopen[PlayerNumber()])
			break;
		//Draw the framework of the menu here
		for(i=0;i<6&&StrCmp(weaponstats[machine][i][2],"");i++)
		{
			setfont("SMALLFONT");
			//TinyName (notice the l: so we can call it from LANGUAGE)
			hudmessage(l:weaponstats[machine][i+listoffset][2];HUDMSG_PLAIN,0,CR_GOLD,71.1,productcursorcoords[i]+13.0,0.03);
			//Icon
			setfont(weaponstats[machine][i+listoffset][3]);
			hudmessage(s:"A";HUDMSG_PLAIN,0,CR_UNTRANSLATED,71.1,productcursorcoords[i]+14.0,0.03);
			setfont("SHOPFONT");
			hudmessage(s:"3";HUDMSG_PLAIN,0,CR_UNTRANSLATED,54.1,productcursorcoords[i],0.03);
		}
		setfont("SMALLFONT");
		hudmessage(s:"Buy it:";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[0]+24.0,326.1,0.03);
		hudmessage(s:"Sell it:";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[1]+24.0,326.1,0.03);
		hudmessage(s:"Fix it:";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[2]+24.0,326.1,0.03);
		hudmessage(s:"AddOn:";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[3]+24.0,326.1,0.03);
		hudmessage(s:"Cash:";HUDMSG_PLAIN,0,CR_UNTRANSLATED,552.1,326.1,0.03);
		hudmessage(i:CheckInventory("ColdHardCash"),s:" cr";HUDMSG_PLAIN,0,CR_UNTRANSLATED,548.1,375.1,0.03);
		//Machine name
		if(machine==8)
			setfont("HELLSHOP");
		else
			setfont("DANCOOKFONT3");
		hudmessage(l:machinedesc[machine][0];HUDMSG_PLAIN,0,CR_GREEN,232.1,productcursorcoords[0]+3.0,0.03);
		//Machine description
		setfont("SMALLFONT");
		hudmessage(l:machinedesc[machine][1];HUDMSG_PLAIN,0,CR_GOLD,232.1,productcursorcoords[0]+200.0,0.03);
		setfont("SHOPFONT");
		hudmessage(s:"A";HUDMSG_PLAIN|HUDMSG_ALPHA,0,CR_UNTRANSLATED,320.0,200.0,0.03,0.75);
		delay(1);
	}
	choicedelay=1;
	while(menuopen[PlayerNumber()])
	{
		if(GetActorProperty(0,APROP_Health)<=0)
		{
			CloseMenu();
			terminate;
		}
		setfont("SHOPFONT");
		hudmessage(s:"1";HUDMSG_PLAIN,0,CR_UNTRANSLATED,54.1,productcursorcoords[productcursorpos-listoffset],0.03);
		hudmessage(s:"2";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[actioncursorpos],316.1,0.03);
		//Draw the framework of the menu here
		for(i=0;i<6&&StrCmp(weaponstats[machine][i][2],"");i++)
		{
			setfont("SMALLFONT");
			//TinyName (notice the l: so we can call it from LANGUAGE)
			hudmessage(l:weaponstats[machine][i+listoffset][2];HUDMSG_PLAIN,0,CR_GOLD,71.1,productcursorcoords[i]+13.0,0.03);
			//Icon
			setfont(weaponstats[machine][i+listoffset][3]);
			hudmessage(s:"A";HUDMSG_PLAIN,0,CR_UNTRANSLATED,71.1,productcursorcoords[i]+14.0,0.03);
			setfont("SHOPFONT");
			hudmessage(s:"3";HUDMSG_PLAIN,0,CR_UNTRANSLATED,54.1,productcursorcoords[i],0.03);
		}
		setfont("SMALLFONT");
		hudmessage(s:"Buy it:";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[0]+24.0,326.1,0.03);
		hudmessage(s:"Sell it:";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[1]+24.0,326.1,0.03);
		hudmessage(s:"Fix it:";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[2]+24.0,326.1,0.03);
		if(machine==8)
			hudmessage(s:"BLEED:";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[3]+24.0,326.1,0.03);
		else
			hudmessage(s:"AddOn:";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[3]+24.0,326.1,0.03);
		hudmessage(s:"Cash:";HUDMSG_PLAIN,0,CR_UNTRANSLATED,552.1,326.1,0.03);
		hudmessage(i:weaponamounts[machine][productcursorpos][0],s:" cr";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[0]+20.0,375.1,0.03);
		hudmessage(i:weaponamounts[machine][productcursorpos][1],s:" cr";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[1]+20.0,375.1,0.03);
		hudmessage(i:weaponamounts[machine][productcursorpos][2],s:" cr";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[2]+20.0,375.1,0.03);
		if(machine==8)
			hudmessage(i:weaponamounts[machine][productcursorpos][3],s:" HP";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[3]+20.0,375.1,0.03);
		else
			hudmessage(i:weaponamounts[machine][productcursorpos][3],s:" cr";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[3]+20.0,375.1,0.03);
		hudmessage(i:CheckInventory("ColdHardCash"),s:" cr";HUDMSG_PLAIN,0,CR_UNTRANSLATED,548.1,375.1,0.03);
		//Wordy description
		if(machine==8)
			setfont("HELLSHOP");
		else
			setfont("DANCOOKFONT3");
		hudmessage(l:weaponstats[machine][productcursorpos][4];HUDMSG_PLAIN,0,CR_GREEN,232.1,productcursorcoords[0]+3.0,0.03);
		//Basic stats
		setfont("SMALLFONT");
		hudmessage(l:weaponstats[machine][productcursorpos][5];HUDMSG_PLAIN,0,CR_GOLD,232.1,productcursorcoords[0]+200.0,0.03);
		setfont("SHOPFONT");
		hudmessage(s:"1";HUDMSG_PLAIN,0,CR_UNTRANSLATED,54.1,productcursorcoords[productcursorpos-listoffset],0.03);
		hudmessage(s:"2";HUDMSG_PLAIN,0,CR_UNTRANSLATED,actioncursorcoords[actioncursorpos],316.1,0.03);
		hudmessage(s:"A";HUDMSG_PLAIN|HUDMSG_ALPHA,0,CR_UNTRANSLATED,320.0,200.0,0.03,0.75);
		input=GetPlayerInput(-1,INPUT_BUTTONS);
		oldinput=GetPlayerInput(-1,INPUT_OLDBUTTONS);
		if(!input && oldinput)
		{
			movedelay=0;
			moveloop=0;
			choicedelay=0;
		}
		if(!movedelay)
		{
			if(input)
				movestart=1;
			if(input & BT_USE||input & BT_ZOOM)
			{
				CloseMenu();
				terminate;
			}
			if(input & BT_BACK) {
				productcursorpos++;
				PlaySound(0, "shop/movevert"); }
			if(input & BT_FORWARD) {
				productcursorpos--;
				PlaySound(0, "shop/movevert"); }
			if(input & BT_RIGHT||input & BT_MOVERIGHT) {
				actioncursorpos++;
				PlaySound(0, "shop/movehorz"); }
			if(input & BT_LEFT||input & BT_MOVELEFT) {
				actioncursorpos--;
				PlaySound(0, "shop/movehorz"); }
			if(productcursorpos>listlength-1)
				productcursorpos=0;
			if(productcursorpos<0)
				productcursorpos=listlength-1;
			if(actioncursorpos>3)
				actioncursorpos=0;
			if(actioncursorpos<0)
				actioncursorpos=3;
			listoffset=productcursorpos-5;
			if(listoffset<0)
				listoffset=0;
		}
		if(movestart)
		{
			if(moveloop)
				movedelay=3;
			else
				movedelay=12;
			movestart=0;
			moveloop=1;
		}
		if(!choicedelay&&input & BT_ATTACK)
		{
			choicedelay=1;
			setfont("SMALLFONT");
			//Hell machine works differently
			if(machine==8)
			{
				//Buy
				if(actioncursorpos==0)
				{
					//Can't afford it
					if(CheckInventory("ColdHardCash")<weaponamounts[machine][productcursorpos][actioncursorpos])
					{
						PlaySound(0, "shop/error");
						Print(s:"fOul vAgrANt! BEGONE wiTh yoU!");
					}
					//Already upgraded
					else if(CheckInventory(weaponstats[machine][productcursorpos][0])>=weaponamounts[machine][productcursorpos][6])
					{
						PlaySound(0, "shop/error");
						Print(l:"You've already got that upgrade!");
					}
					//Can buy it
					else
					{
						PlaySound(0, "shop/upgrade");
						Print(l:"we hOpE yOu like it...hehe");
						GiveInventory(weaponstats[machine][productcursorpos][0],
							weaponamounts[machine][productcursorpos][4]);
						TakeInventory("ColdHardCash",weaponamounts[machine][productcursorpos][actioncursorpos]);
					}
				}
				//Sell
				else if(actioncursorpos==1)
				{
					PlaySound(0, "shop/error");
					Print(s:"Can't sell this!");
				}
				//Fix
				else if(actioncursorpos==2)
				{
				}
				//Blood Price
				else if(actioncursorpos==3)
				{
					//Can't afford it
					if(GetActorProperty(0,APROP_Health)<=weaponamounts[machine][productcursorpos][actioncursorpos])
					{
						PlaySound(0, "shop/error");
						Print(s:"iT woULd bE a SHAME to kILL YOU lIKe ThiS.\nTRY AgaIn LatER.");
					}
					//Already upgraded
					else if(CheckInventory(weaponstats[machine][productcursorpos][0]))
					{
						PlaySound(0, "shop/error");
						Print(l:"You've already got that upgrade!");
					}
					//Can buy it
					else
					{
						PlaySound(0, "shop/bleed");
						Print(l:"we hOpE yOu like it...hehe");
						GiveInventory(weaponstats[machine][productcursorpos][0],1);
						Thing_Damage2(0,weaponamounts[machine][productcursorpos][actioncursorpos],"BloodDonor");
					}
				}
			}
			else
			{
				//Buy
				if(actioncursorpos==0)
				{
					//Can't afford it
					if(CheckInventory("ColdHardCash")<weaponamounts[machine][productcursorpos][actioncursorpos])
					{
						PlaySound(0, "shop/error");
						Print(l:"SHOP_NOMONEY");
					}
					//Health (special case)
					else if(!StrCmp(weaponstats[machine][productcursorpos][0],"Health"))
					{
						//Health full
						if(GetActorProperty(0,APROP_Health)>=GetActorProperty(0,APROP_SpawnHealth))
						{
							PlaySound(0, "shop/error");
							Print(l:"TXT_HAVEENOUGH");
						}
						//Can buy it
						else
						{
							PlaySound(0, "shop/buy");
							Print(l:"SHOP_ITEMBOUGHT");
							HealThing(10);
							TakeInventory("ColdHardCash",weaponamounts[machine][productcursorpos][actioncursorpos]);
						}
					}
					//Armor 1 (special case)
					else if(!StrCmp(weaponstats[machine][productcursorpos][0],"CHCBasicArmor"))
					{
						//Armor full
						if(CheckInventory("BasicArmor")>=100)
						{
							PlaySound(0, "shop/error");
							Print(l:"TXT_HAVEENOUGH");
						}
						//Can buy it
						else
						{
							PlaySound(0, "shop/buy");
							Print(l:"SHOP_ITEMBOUGHT");
							GiveInventory(weaponstats[machine][productcursorpos][0],
							weaponamounts[machine][productcursorpos][4]);
							TakeInventory("ColdHardCash",weaponamounts[machine][productcursorpos][actioncursorpos]);
						}
					}
					//Armor 2 (special case)
					else if(!StrCmp(weaponstats[machine][productcursorpos][0],"CHCAdvancedArmor"))
					{
						//Armor full
						if(CheckInventory("BasicArmor")>=200)
						{
							PlaySound(0, "shop/error");
							Print(l:"TXT_HAVEENOUGH");
						}
						//Can buy it
						else
						{
							PlaySound(0, "shop/buy");
							Print(l:"SHOP_ITEMBOUGHT");
							GiveInventory(weaponstats[machine][productcursorpos][0],
							weaponamounts[machine][productcursorpos][4]);
							TakeInventory("ColdHardCash",weaponamounts[machine][productcursorpos][actioncursorpos]);
						}
					}
					//Inventory full
					else if(CheckInventory(weaponstats[machine][productcursorpos][0])>=weaponamounts[machine][productcursorpos][6])
					{
						PlaySound(0, "shop/error");
						Print(l:"TXT_HAVEENOUGH");
					}
					//Can buy it
					else
					{
						PlaySound(0, "shop/buy");
							Print(l:"SHOP_ITEMBOUGHT");
						if(weaponamounts[machine][productcursorpos][7]&&
						!CheckInventory(weaponstats[machine][productcursorpos][0])&&
						!GetCVAR("neverswitchonpickup"))
						{
							GiveInventory(weaponstats[machine][productcursorpos][0],
							weaponamounts[machine][productcursorpos][4]);
							SetWeapon(weaponstats[machine][productcursorpos][0]);
						}
						else
							GiveInventory(weaponstats[machine][productcursorpos][0],
							weaponamounts[machine][productcursorpos][4]);
						TakeInventory("ColdHardCash",weaponamounts[machine][productcursorpos][actioncursorpos]);
					}
				}
				//Sell
				else if(actioncursorpos==1)
				{
					//Health (special case)
					if(!StrCmp(weaponstats[machine][productcursorpos][0],"Health"))
					{
						//Health too low
						if(GetActorProperty(0,APROP_Health)<=5)
						{
							PlaySound(0, "shop/error");
							Print(s:"Dude, you NEED that blood!");
						}
						//Can sell it
						else
						{
							PlaySound(0, "shop/bleed");
							Print(s:"Thank you for your kind donation!");
							Thing_Damage2(0,5,"BloodDoner");
							GiveInventory("ColdHardCash",weaponamounts[machine][productcursorpos][actioncursorpos]);
						}
					}
					//Can't be sold
					else if(weaponamounts[machine][productcursorpos][5]==0)
					{
						PlaySound(0, "shop/error");
						Print(s:"Can't sell this!");
					}
					//Inventory empty
					else if(CheckInventory(weaponstats[machine][productcursorpos][0])==0)
					{
						PlaySound(0, "shop/error");
						Print(l:"SHOP_NOSELL");
					}
					//Can sell it
					else
					{
						PlaySound(0, "shop/sell");
						Print(l:"SHOP_ITEMSOLD");
						//Take the item
						TakeInventory(weaponstats[machine][productcursorpos][0],
							weaponamounts[machine][productcursorpos][5]);
						//Take the upgrade
						TakeInventory(weaponstats[machine][productcursorpos][1],
							weaponamounts[machine][productcursorpos][5]);
						//Glock (special case)
						if(!StrCmp(weaponstats[machine][productcursorpos][0],"SinglePistol")&&CheckInventory("DualPistols"))
						{
							TakeInventory("DualPistols",1);
							GiveInventory("ColdHardCash",50);
						}
						//MO Blaster (special case
						if(!StrCmp(weaponstats[machine][productcursorpos][0],"MOBlaster"))
							TakeInventory("MoBlasterAmmo",9999);
						//Give some money
						GiveInventory("ColdHardCash",weaponamounts[machine][productcursorpos][actioncursorpos]);
					}
				}
				//Fix
				else if(actioncursorpos==2)
				{
				}
				//Upgrade
				else if(actioncursorpos==3)
				{
					//Can't afford it
					if(CheckInventory("ColdHardCash")<weaponamounts[machine][productcursorpos][actioncursorpos])
					{
						PlaySound(0, "shop/error");
						Print(l:"SHOP_NOMONEY");
					}
					//No upgrade for this item
					else if(!StrCmp(weaponstats[machine][productcursorpos][1],""))
					{
						PlaySound(0, "shop/error");
						Print(s:"No upgrade available!");
					}
					//Don't own the base item
					else if(!CheckInventory(weaponstats[machine][productcursorpos][0]))
					{
						PlaySound(0, "shop/error");
						Print(s:"You need the item before you can upgrade!");
					}
					//Already upgraded
					else if(CheckInventory(weaponstats[machine][productcursorpos][1]))
					{
						PlaySound(0, "shop/error");
						Print(l:"You've already got that upgrade!");
					}
					//Can buy it
					else
					{
						PlaySound(0, "shop/upgrade");
						Print(l:"SHOP_UPGRADEBOUGHT");
						GiveInventory(weaponstats[machine][productcursorpos][1],1);
						//Glock (special case)
						if(!StrCmp(weaponstats[machine][productcursorpos][0],"SinglePistol"))
							GiveInventory("DualPistols",1);
						TakeInventory("ColdHardCash",weaponamounts[machine][productcursorpos][actioncursorpos]);
					}
				}
			}
		}
		if(movedelay)
			movedelay--;
		delay(1);
	}
	delay(5);
	CloseMenu();
}
function void CloseMenu(void)
{
	PlaySound(0, "shop/close");
	setplayerproperty(0,0,PROP_TOTALLYFROZEN);
	menuopen[PlayerNumber()]=0;
}