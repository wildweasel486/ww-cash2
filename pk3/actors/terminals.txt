// Weapon terminals. They take your money, they give you stuff.

// LEVEL 1 TERMINAL: TOOLS AND FIRST AID
ACTOR CashTerminalLevel1 : SwitchableDecoration replaces Chainsaw
{
	+USESPECIAL
	+SHOOTABLE
	+NOBLOOD
	+DONTDRAIN
	Scale 0.4
	Radius 16
	Height 56
	Health 10000
	Mass 0x7fffffff
	DamageFactor Telefrag,0
	PainChance 256
	+LAXTELEFRAGDMG
	States
	{
	Spawn:
		DIS1 A 0 NODELAY Thing_SetSpecial(0,226,9001,0,1)
		"####" AAAAA 0 A_SpawnItemEx("CHCSmallCoin",frandom(-16,16),frandom(-16,16),0,0,0,0,0,SXF_NOCHECKPOSITION, 32)
		"####" AAA 0 A_SpawnItemEx("CHCMediumCoin",frandom(-16,16),frandom(-16,16),0,0,0,0,0,SXF_NOCHECKPOSITION, 96)
		"####" A 0 A_SpawnItemEx("CHCLargeCoin",frandom(-16,16),frandom(-16,16),0,0,0,0,0,SXF_NOCHECKPOSITION, 160)
	See:
		"####" A -1
		Stop
	Pain.Thief:
		"####" A 1 A_JumpIfHealthLower(9800,"Broken.Thief")
		Goto See
	Pain:
		"####" A 1 A_JumpIfHealthLower(9800,"Broken")
		Goto See
	Broken.Thief:
		DIS1 AAAAA 0 A_SpawnItemEx("CHCLargeCoin",0,0,8,random(-2,2),random(-2,2),2,random(0,359),SXF_NOCHECKPOSITION, 32)
		DIS1 AAA 0 A_SpawnItemEx("CHCSmallBill",0,0,8,random(-2,2),random(-2,2),2,random(0,359),SXF_NOCHECKPOSITION, 96)
		DIS1 A 0 A_SpawnItemEx("CHCMediumBill",0,0,8,random(-2,2),random(-2,2),2,random(0,359),SXF_NOCHECKPOSITION, 160)
	Broken:
		DIS1 A 0 Thing_SetSpecial(0,0,0,0,0)
		BEXP B 0 A_ChangeFlag(USESPECIAL,0)
		BEXP B 0 A_SetScale(1.5)
		BEXP B 0 A_ChangeFlag(SHOOTABLE,0)
		BEXP C 5 BRIGHT A_PlaySound("world/barrelx")
		BEXP DE 5 BRIGHT
		Stop
	}
}

// LEVEL 2 TERMINAL: HANDGUNS AND OTHER SIDEARMS
ACTOR CashTerminalLevel2 : CashTerminalLevel1 replaces Shotgun
{
	States
	{
	Spawn:
		DIS2 A 0 NODELAY Thing_SetSpecial(0,226,9001,0,2)
		Goto Super::Spawn+1
	}
}

// LEVEL 3 TERMINAL: RIFLES
ACTOR CashTerminalLevel3 : CashTerminalLevel1 replaces SuperShotgun
{
	States
	{
	Spawn:
		DIS3 A 0 NODELAY Thing_SetSpecial(0,226,9001,0,3)
		Goto Super::Spawn+1
	}
}

// LEVEL 4 TERMINAL: AUTOMATICS
ACTOR CashTerminalLevel4 : CashTerminalLevel1 replaces Chaingun
{
	States
	{
	Spawn:
		DIS4 A 0 NODELAY Thing_SetSpecial(0,226,9001,0,4)
		Goto Super::Spawn+1
	}
}

// LEVEL 5 TERMINAL: EXPLOSIVES
ACTOR CashTerminalLevel5 : CashTerminalLevel1 replaces RocketLauncher
{
	States
	{
	Spawn:
		DIS5 A 0 NODELAY Thing_SetSpecial(0,226,9001,0,5)
		Goto Super::Spawn+1
	}
}

// LEVEL 6 TERMINAL: ENERGY WEAPONS
ACTOR CashTerminalLevel6 : CashTerminalLevel1 replaces PlasmaRifle
{
	States
	{
	Spawn:
		DIS6 A 0 NODELAY Thing_SetSpecial(0,226,9001,0,6)
		Goto Super::Spawn+1
	}
}

// LEVEL 7 TERMINAL: POWER WEAPONS
ACTOR CashTerminalLevel7 : CashTerminalLevel1 replaces BFG9000
{
	States
	{
	Spawn:
		DIS7 A 0 NODELAY Thing_SetSpecial(0,226,9001,0,7)
		Goto Super::Spawn+1
	}
}

// PENTAGRAM TERMINAL: HELL
ACTOR CashTerminalLevelPentagram : CashTerminalLevel1 replaces Berserk
{
	States
	{
	Spawn:
		DIS8 A 0 NODELAY Thing_SetSpecial(0,226,9001,0,8)
		"####" A 0 A_SpawnItemEx("ArtiHypostim",frandom(-16,16),frandom(-16,16),0,0,0,0,0,SXF_NOCHECKPOSITION)
		Goto Super::Spawn+1
	}
}

// AMMO TERMINAL: PLENTY OF BOOLIT!
ACTOR CashTerminalLevelAmmo : CashTerminalLevel1
{
	States
	{
	Spawn:
		DIS9 A 0 NODELAY Thing_SetSpecial(0,226,9001,0,9)
		Goto Super::Spawn+1
	}
}

// ARMOR TERMINAL: PROTECTION, MAN!
ACTOR CashTerminalLevelArmor : CashTerminalLevel1
{
	States
	{
	Spawn:
		DISA A 0 NODELAY Thing_SetSpecial(0,226,9001,0,10)
		Goto Super::Spawn+1
	}
}

// This SHOULD spawn the same terminal on both armors
ACTOR ArmorSpawner : RandomSpawner replaces GreenArmor
{
	DropItem "CashTerminalLevelArmor"
}
ACTOR ArmorSpawner2 : RandomSpawner replaces BlueArmor
{
	DropItem "CashTerminalLevelArmor"
}