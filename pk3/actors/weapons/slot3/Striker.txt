// Model Base: W1LLDAB3AST, DeadPixel, SilentAssassin12, CIgano

ACTOR StrikerLoaded : Ammo
{
	Inventory.MaxAmount 20
	+IGNORESKILL
}

ACTOR StrikerAutoShotgun : CashWeapon
{
	Weapon.AmmoType1 "StrikerLoaded"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "ShotgunAmmo"
	Weapon.AmmoGive2 10
	Weapon.AmmoUse2 1
	Weapon.SlotNumber 3
	Radius 32
	Height 20
	Scale 0.6
	Tag "$WWTWEP3NAME"
	Inventory.PickupMessage "$WWTWEP3PICK"
	Inventory.PickupSound "Striker/Pickup"
	Inventory.Icon "STKPA0"
	+Weapon.AMMO_CHECKBOTH
	+Weapon.NoAlert
	States
	{
	Spawn:
		STKP A -1
		Stop
	Select:
	ReadyAnim:
		TNT1 A 15
		STKG E 1 Offset(90,91)
		STKG E 1 Offset(75,81)
		STKG E 1 Offset(60,71)
		STKG E 1 Offset(48,61)
		STKG E 1 Offset(38,51) A_PlaySound("Striker/Pickup", 5)
		STKG E 1 Offset(29,43)
		STKG E 1 Offset(20,36)
		STKG A 1 Offset(11,35)
		STKG A 1 Offset(6,33) A_PlaySound("Striker/Ready", 6)
		STKG A 1 Offset(3,32)
	Ready:
		STKG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		STKG A 0 A_StopSound(CHAN_WEAPON)
		"####" A 1 Offset(3,32)
		"####" A 1 Offset(6,33)
		"####" A 1 Offset(11,35)
		"####" A 1 Offset(20,36)
		"####" A 1 Offset(29,43)
		"####" A 1 Offset(38,51)
		"####" A 1 Offset(48,61)
		"####" A 1 Offset(60,71)
		"####" A 1 Offset(75,81)
		"####" A 1 Offset(90,91)
		TNT1 A 0 A_Lower
		Wait
	Fire:
		STKG A 0 A_JumpIfInventory("StrikerLoaded",1,1)
		Goto DryFire
		STKG B 0 A_PlayWeaponSound("Striker/Fire")
		STKG B 0 A_GunFlash
		STKG B 0 A_AlertMonsters
		STKG B 1 BRIGHT A_FireBullets(3.0, 3.0, 8, 4, "BulletPuff", FBF_USEAMMO)
		STKG C 1 BRIGHT Offset(4,34) A_SetPitch(pitch - 2.0)
		STKG D 1 BRIGHT Offset(12,38) A_SetPitch(pitch - 1.5)
		STKG D 1 BRIGHT Offset(24,38) A_SetPitch(pitch - 1.0)
		STKG A 1 Offset(16,40) A_SetPitch(pitch - 0.5)
		STKG A 1 Offset(8,36) A_SetPitch(pitch + 0.9)
		STKG A 1 Offset(4,34) A_SetPitch(pitch + 0.7)
		STKG A 0 A_Refire
		STKG A 1 Offset(2,32) A_SetPitch(pitch + 0.5)
		STKG A 1 A_SetPitch(pitch + 0.3)
		Goto Ready
	Dryfire:
		STKG A 0 A_JumpIf(CallACS("Autoreload")==1,"Reload")
		STKG A 1 A_PlaySound("Dryfire/Rifle",6)
		STKG A 1 Offset(2,34)
		STKG A 1 Offset(1,33)
		Goto Ready
	Reload:
		STKG A 0 A_JumpIfInventory("StrikerLoaded", 0, 2)
		STKG A 0 A_JumpIfInventory("ShotgunAmmo", 1, 2)
		STKG A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready
		STKG E 1 Offset(3,34) A_PlaySound("Striker/Clipout", 5)
		STKG E 1 Offset(4,37)
		STKG E 1 Offset(5,39)
		STKG E 1 Offset(6,42)
		STKG E 1 Offset(7,45)
		STKG E 1 Offset(8,48)
		STKG E 1 Offset(9,52)
		STKG E 1 Offset(10,56)
		STKG E 1 Offset(11,60)
		STKG E 1 Offset(12,65)
		STKG E 1 Offset(13,70)
		STKG E 1 Offset(14,75)
		STKG E 1 Offset(15,86)
		STKG E 1 Offset(16,92)
		STKG E 1 Offset(17,98)
		STKG E 1 Offset(18,103)
		STKG E 1 Offset(19,106)
		STKG E 1 Offset(20,109)
		STKG E 1 Offset(22,112)
		STKG E 1 Offset(25,109)
	ReloadWork:
		STKG E 0 A_TakeInventory("ShotgunAmmo", 1)
		STKG E 0 A_GiveInventory("StrikerLoaded", 1)
		STKG E 0 A_JumpIfInventory("StrikerLoaded", 0, "ReloadFinish")
		STKG E 0 A_JumpIfInventory("ShotgunAmmo", 1, "ReloadWork")
	ReloadFinish:
		STKG E 1 Offset(28,100) A_PlaySound("Striker/Clipin", 5)
		STKG E 1 Offset(29,90)
		STKG E 1 Offset(28,79)
		STKG E 1 Offset(27,70)
		STKG E 1 Offset(26,61)
		STKG E 1 Offset(24,53)
		STKG E 1 Offset(21,47)
		STKG E 1 Offset(26,44)
		STKG E 1 Offset(18,38)
		STKG E 1 Offset(12,33)
		STKG A 1 Offset(6,32)
		STKG A 1 Offset(1,32)
		STKG A 1 Offset(-2,33) A_PlaySound("Striker/Ready", 5)
		STKG A 1 Offset(-3,34)
		STKG A 1 Offset(-1,33)
		Goto Ready
	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	}
}