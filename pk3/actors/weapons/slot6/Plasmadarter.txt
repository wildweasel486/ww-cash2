ACTOR MOBlasterAmmo : Ammo
{
	Inventory.MaxAmount 200
	Inventory.Icon BLSPA0
}

ACTOR MOBlaster : CashWeapon
{
	Weapon.Kickback 70
	Weapon.AmmoType "MOBlasterAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 200
	Weapon.SlotNumber 6
	Tag "$SHOP6_ITEM1NAME"
	+NOAUTOFIRE
	+AMMO_OPTIONAL
	States
	{
	Ready:
		BLST A 1 A_WeaponReady
		Loop
	Deselect:
		BLST A 1 A_Lower
		Loop
	Select:
		BLST A 1 A_Raise
		Loop
	Fire:
		BLST A 0 A_JumpIfNoAmmo("Dryfire")
		BLSF A 0 A_PlayWeaponSound("blaster/fire")
		BLSF A 2 BRIGHT A_FireCustomMissile("BlasterShot",0,1,10,0)
		BLST CED 1 A_AlertMonsters
		BLST CBA 1 A_WeaponReady
		Goto Ready
    Flash:
		TNT1 A 1 A_Light2
		TNT1 A 1 A_Light1
		Goto LightDone
	Dryfire:
		BLST A 1 A_PlayWeaponSound("empty/pistol")
		Goto Ready
	}
}

ACTOR BlasterShotSpark
{
	Health 15
	radius 3
	height 6
	speed .1
	RenderStyle ADD
	Alpha 0.95
	Mass 0
	Scale 0.25
	+missile
	+doombounce
	+FLOORCLIP
	+DONTSPLASH
	States
	{
	Spawn:
		LAS1 A 1 BRIGHT A_FadeOut(0.05)
		Loop
	}
}

ACTOR BlasterShot : FastProjectile
{
	PROJECTILE
	Damage 5
	Speed 40
	Height 8
	Radius 8
	Scale 0.5
	RenderStyle Add
	Alpha 0.95
	MissileHeight 8
	MissileType "BlasterShotTrail"
    DeathSound "blaster/explode"
	States
	{
	Spawn:
		LAS1 A 1 Bright
		Loop
    Death:
        LAS1 A 0
        LAS1 A 2 A_SpawnDebris("BlasterShotSpark")
        LAS1 BCDEF 2
        Stop
	}
}

ACTOR BlasterShotTrail
{
	PROJECTILE
	Height 8
	Radius 8
	Scale 0.45
	RenderStyle Add
	Alpha 0.95
	States
	{
	Spawn:
		LAS1 A 1 BRIGHT A_FadeOut(0.3)
		Loop
	}
}