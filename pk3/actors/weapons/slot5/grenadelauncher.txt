// Cooke GLP-140 (Milkor MGL-140)
// Model credits:
// - Ultimate Bastard
// - Felipe Paiva
// - Teh Snake
// - Inph!n!tY
// - TheLama
// - DeadPixel
// - Racer445
// Sound credits:
// - Vabadus
// - Yzhamua
// - Bmaczero
// - Soldier of Fortune 2

ACTOR MGLUpgrade : Inventory {}

ACTOR MGLLoaded : Ammo
{
	Inventory.MaxAmount 6
	+IGNORESKILL
}

ACTOR MGL140 : CashWeapon
{
	Scale 0.7
	Weapon.SlotNumber 7
	Weapon.AmmoType1 "MGLLoaded"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "GrenadeAmmo"
	Weapon.AmmoUse2 1
	Weapon.AmmoGive2 6
	Inventory.Icon "MGLPA0"
	Inventory.PickupSound "MGL/Close"
	Inventory.PickupMessage "$WWTWEP7PICK"
	Tag "$WWTWEP7NAME"
	+Weapon.NoAutoAim
	+Weapon.NoAutoFire
	+Weapon.AMMO_CHECKBOTH
	+Weapon.NoAlert
	States
	{
	Spawn:
		MGLP A -1
		Stop
	Select:
	ReadyAnim:
		TNT1 A 15
		MGLG A 1 Offset(90,91)
		"####" A 1 Offset(75,81)
		"####" A 1 Offset(60,71)
		"####" A 1 Offset(48,61) A_PlaySound("MGL/Close", 5)
		"####" A 1 Offset(38,51)
		"####" A 1 Offset(29,43)
		"####" A 1 Offset(20,36)
		"####" A 1 Offset(11,35)
		"####" A 1 Offset(6,33)
		"####" A 1 Offset(3,32)
	Ready:
		MGLG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		"####" A 0 A_StopSound(CHAN_WEAPON)
		"####" A 1 Offset(3,32)
		"####" A 1 Offset(6,33)
		"####" A 1 Offset(11,35)
		"####" A 1 Offset(20,36)
		"####" A 1 Offset(29,43)
		"####" A 1 Offset(38,51)
		"####" A 1 Offset(48,61)
		"####" A 1 Offset(60,71)
		"####" A 1 Offset(75,81)
		"####" A 1 Offset(90,91)
		TNT1 A 0 A_Lower
		Wait
	Fire:
		MGLG A 0 A_JumpIfInventory("MGLLoaded",1,1)
		Goto DryFire
		MGLG A 0 A_PlayWeaponSound("MGL/Fire")
		MGLG A 0 A_JumpIfInventory("MGLUpgrade", 1, "FireStable")
		MGLG A 1 Bright Offset(3,35) A_FireCustomMissile("LauncherGrenade", 0, 1, 8)
	FireContinue:
		MGLG B 1 Bright Offset(6,38) A_SetPitch(pitch - 1.5)
		MGLG C 1 Bright Offset(9,42) A_SetPitch(pitch - 1.2)
		MGLG A 1 Bright Offset(12,46) A_SetPitch(pitch - 0.9)
		MGLG A 1 Offset(13,48) A_SetPitch(pitch - 0.6)
		MGLG A 1 Offset(12,45) A_SetPitch(pitch - 0.3)
		MGLG A 1 Offset(10,42) A_SetPitch(pitch + 0.1)
		MGLG A 1 Offset(8,39) A_SetPitch(pitch + 0.2)
		MGLG A 1 Offset(6,36) A_SetPitch(pitch + 0.3)
		MGLG A 1 Offset(3,34) A_SetPitch(pitch + 0.4)
		MGLG A 1 Offset(1,33) A_SetPitch(pitch + 0.5)
		MGLG A 1 Offset(-1,31) A_SetPitch(pitch + 0.3)
		MGLG A 1 Offset(-1,32) A_SetPitch(pitch + 0.1)
		Goto Ready
	FireStable:
		MGLG A 1 Bright Offset(3,35) A_FireCustomMissile("LauncherGrenadeStable", 0, 1, 8)
		Goto FireContinue
	Dryfire:
		MGLG A 0 A_JumpIf(CallACS("Autoreload")==1,"Reload")
		MGLG A 1 A_PlaySound("Dryfire/Heavy",6)
		MGLG A 1 Offset(2,34)
		MGLG A 1 Offset(1,33)
		Goto Ready
	Reload:
		MGLG A 0 A_JumpIfInventory("MGLLoaded", 6, 2)
		MGLG A 0 A_JumpIfInventory("GrenadeAmmo", 1, 2)
		MGLG A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready
		MGLG A 1 A_PlaySound("MGL/Open", 5)
		MGLG A 1 Offset(1,35)
		MGLG A 1 Offset(2,38)
		MGLG A 1 Offset(3,41)
		MGLG A 1 Offset(4,44)
		MGLG A 1 Offset(5,47)
		MGLG A 1 Offset(6,50)
		MGLG A 1 Offset(7,52)
		MGLG A 1 Offset(8,54)
		MGLG A 1 Offset(9,56)
		MGLG A 1 Offset(10,57)
		MGLG A 1 Offset(11,58)
		MGLG A 1 Offset(13,59)
		MGLG A 1 Offset(14,60)
	ReloadWork:
		MGLG A 0 A_TakeInventory("GrenadeAmmo", 1)
		MGLG A 0 A_GiveInventory("MGLLoaded", 1)
		MGLG A 1 Offset(12,65) A_PlaySound("MGL/Load", 5)
		MGLG A 1 Offset(11,66) //A_SpawnItemEx("40mmCasing",24,-12,24,0,0,0,random(-55,-80),SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		MGLG A 1 Offset(10,67) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(9,68) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(8,69) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(7,70) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(8,69) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(9,68) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(12,67) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(13,69) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(14,71) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(15,72) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(16,73) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(17,74) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(18,75) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(19,76) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(18,75) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(17,74) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(16,73) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(15,72) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(14,70) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(13,68) A_WeaponReady(WRF_NOBOB)
		MGLG A 1 Offset(12,66) A_WeaponReady(WRF_NOBOB)
		MGLG A 0 A_JumpIfInventory("MGLLoaded", 6, "ReloadFinish")
		MGLG A 0 A_JumpIfInventory("GrenadeAmmo", 1, "ReloadWork")
		Goto ReloadFinish
	ReloadFinish:
		MGLG A 1 Offset(11,57) A_PlaySound("MGL/Close", 5)
		MGLG A 1 Offset(9,50)
		MGLG A 1 Offset(5,45)
		MGLG A 1 Offset(2,39)
		MGLG A 1 Offset(1,35)
		Goto Ready
	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 2 A_Light1
		Goto LightDone
	}
}

ACTOR LauncherGrenade
{
	Radius 4
	Height 3
	Speed 55
	Damage (1)
	Gravity 0.6
	Decal Scorch
	PROJECTILE
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	-NOGRAVITY
	States
	{
	Spawn:
		SGRN A 1
		loop
	Death:
		FRME A 0 A_AlertMonsters
		FRME A 0 A_SetScale(1.75,1.75)
		FRME A 0 A_SetTranslucent(0.75,1)
		FRME A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FRME A 1 bright A_Explode(128,192,1)
		FRME B 1 A_Quake(7,10,0,256,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}

ACTOR LauncherGrenadeStable : LauncherGrenade
{
	Gravity 0.2
}