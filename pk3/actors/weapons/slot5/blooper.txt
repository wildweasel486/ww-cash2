ACTOR BlooperLoaded : Ammo
{
	Inventory.MaxAmount 1
}

ACTOR Blooper : CashWeapon
{
	Weapon.AmmoType1 "BlooperLoaded"
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "40mmAmmo"
	Weapon.SlotNumber 5
	+Weapon.Ammo_Optional
	+Weapon.NoAutoFire
	States
	{
	Spawn:
		HCAN A -1
		Stop
	Select:
		HANG A 1 A_Raise
		Loop
	Deselect:
		HANG A 1 A_Lower
		Loop
	Ready:
		HANG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		HANG A 0 A_JumpIfNoAmmo("Dryfire")
		HANF A 2 BRIGHT A_FireCustomMissile("LauncherGrenade", 0, 1, 8)
		HANG A 1 Offset(1,33)
		HANG A 1 Offset(3,35)
		HANG A 1 Offset(6,38)
		HANG A 1 Offset(11,41)
		HANG A 1 Offset(12,42)
		HANG A 1 Offset(9,40)
		HANG A 1 Offset(6,37)
		HANG A 1 Offset(4,35)
		HANG A 1 Offset(2,33)
		HANG A 1 Offset(1,32)
		Goto Ready
	Dryfire:
		HANG A 1 A_PlaySound("Dryfire/Heavy",6)
		HANG A 1 Offset(2,34)
		HANG A 1 Offset(1,33)
		Goto Ready	
	Reload:
		HANG BCDEFGHIJKLMNOPQRSTUVWX 1
		HANG X 1 A_GiveInventory("BlooperLoaded",1)
		HANG XWVUTSRQPONMLKJIHGFEDCB 1
		Goto Ready
	}
}