ACTOR CHCPDA : CustomInventory
{
	Tag "Quine 5000 PDA"
	Inventory.maxamount 1
	Inventory.icon "PDAICON"
	+inventory.invbar
	States
	{
	Use:
		TNT1 A 0 ACS_NamedExecuteAlways("WWcashActivateVendor",0)
		Fail
	}
}

// ============
// = HYPOSTIM =
// ============

ACTOR ArtiHypostim : HealthPickup
{
	Health 100
	+COUNTITEM
	Inventory.Icon VIALICON
	Inventory.PickupMessage "Picked up a HypoStim."
	Tag "$MISC_HYPOSTIM"
	HealthPickup.AutoUse 3
	Scale 0.25
	States
	{
	Spawn:
		VIAL A -1
		Loop
	}
}


// =================
// = HAND GRENADES =
// =================

ACTOR ArtiHandGrenade : CustomInventory
{
	inventory.maxamount 0x7fffffff
	inventory.icon GRENICON
	inventory.usesound "grenade/throw"
	+Inventory.invbar
	+inventory.keepdepleted
	Inventory.InterHubamount 0x7fffffff
	Inventory.PickupMessage "Picked up a fragmentation grenade."
	Tag "$SHOP5_ITEM2NAME"
	Scale 0.25
	states
	{
	Spawn:
		THRW A -1
		Stop
	Use:
		TNT1 A 0 A_FireCustomMissile("HandGrenade", 0, 0)
		stop
	}
}

ACTOR HandGrenade
{
	Radius 4
	Height 3
	Speed 30
	Scale 0.25
	Damage (1)
	Gravity 0.75
	DamageType "PlayerGrenade"
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+CANBOUNCEWATER
	obituary "%o failed to field %k's fragmenting fastball."
	BounceType Doom
	BounceFactor 0.4
	WallBounceFactor 0.4
	bouncesound "grenade/bounce"
	states
	{
	Spawn:
		THRW A 2 Bright A_Countdown
		THRW BCDEF 2 Bright
		loop
	Death:
		THRW B 35 Bright
		FRME A 0 A_AlertMonsters
		FRME A 0 A_SetScale(1.75,1.75)
		FRME A 0 A_SetTranslucent(0.75,1)
		FRME A 0 A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FRME A 1 bright A_Explode(150,256,1,1,128,15,2)
		FRME B 1 Bright A_Quake(7,10,0,480,0)
		FRME CDEFGHIHJKLMNOPQRS 1 bright
		stop
	}
}

ACTOR ArtiElectricGrenade : ArtiHandGrenade
{
	inventory.icon EGRNICON
	Inventory.PickupMessage "Picked up an electric grenade."
	Tag "$SHOP6_ITEM6NAME"
	Scale 1
	states
	{
	Spawn:
		EGRN C -1
		Stop
	Use:
		TNT1 A 0 A_FireCustomMissile("ElectricGrenade", 0, 0)
		stop
	}
}

ACTOR ElectricGrenade : HandGrenade
{
	Scale 0.55
	DamageType "PlayerElectricGrenade"
	obituary "%o would make a good conductor, says %k."
	states
	{
	Spawn:
		EGRN A 2 Bright A_Countdown
		EGRN BCDEFGH 2 Bright
		loop
	Death:
		EGRN C 35 Bright
		STFL A 0 A_AlertMonsters
		STFL A 0 A_SetTranslucent(0.75,1)
		STFL A 0 A_PlaySound("grenade/elecexplode",0,1.0,0,0.1)
		STFL A 0 Bright A_Quake(7,10,0,512,0)
		STFL A 1 Bright A_Explode(8,192)
		STFL B 1 Bright A_SetScale(0.6)
		STFL C 1 Bright A_Explode(8,200)
		STFL D 1 Bright A_SetScale(0.7)
		STFL E 1 Bright A_Explode(8,208)
		STFL F 1 Bright A_SetScale(0.8)
		STFL G 1 Bright A_Explode(8,216)
		STFL H 1 Bright A_SetScale(0.9)
		STFL I 1 Bright A_Explode(8,224)
		STFL J 1 Bright A_SetScale(1.0)
		STFL K 1 Bright A_Explode(8,232)
		STFL L 1 Bright A_SetScale(1.2)
		STFL M 1 Bright A_Explode(8,240)
		STFL N 1 Bright A_SetScale(1.4)
		STFL O 1 Bright A_Explode(8,248)
		STFL P 1 Bright A_SetScale(1.6)
		STFL A 1 Bright A_Explode(8,256)
		STFL B 1 Bright A_SetScale(1.8)
		STFL C 1 Bright A_Explode(8,264)
		STFL D 1 Bright A_SetScale(2.0)
		STFL E 1 Bright A_Explode(8,272)
		STFL F 1 Bright A_SetScale(2.25)
		STFL G 1 Bright A_Explode(8,280)
		STFL H 1 Bright A_SetScale(2.5)
		STFL I 1 Bright A_Explode(8,288)
		STFL J 1 Bright A_SetScale(2.75)
		STFL K 1 Bright A_Explode(8,296)
		STFL L 1 Bright A_SetScale(3.0)
		STFL M 1 Bright A_Explode(8,304)
		STFL N 1 Bright A_SetScale(3.25)
		STFL O 1 Bright A_Explode(8,312)
		STFL P 1 Bright A_SetScale(3.5)
		stop
	}
}

// =================
// = CARE PACKAGES =
// =================

ACTOR ArtiCarePackageMedical : CustomInventory
{
	Tag "Care Package Beacon (Medical)"
	Inventory.MaxAmount 0x7fffffff
	Inventory.InterHubamount 0x7fffffff
	+inventory.invbar
	Inventory.icon BEAC1ICN
	States
	{
	Use:
		TNT1 A 0 A_FireCustomMissile("CarePackageBeaconMedicalThrown", 0, 0)
		Stop
	}
}

ACTOR CarePackageBeaconMedicalThrown
{
	PROJECTILE
	-NOGRAVITY
	Speed 10
	Scale 0.45
	States
	{
	Spawn:
		BEAC A -1
		Stop
	Death:
		TNT1 A 1 A_SpawnItemEx("CarePackageBeaconMedical", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR CarePackageBeaconMedical
{
	States
	{
	Spawn:
		BEAC B 35
		BEAC A 17 A_PlaySound("beacon/beep")
		BEAC B 18
		BEAC A 17 A_PlaySound("beacon/beep")
		BEAC B 18
		BEAC A 17 A_PlaySound("beacon/beep")
		BEAC B 18
		BEAC A 17 A_PlaySound("beacon/beep")
		BEAC B 18
		BEAC A 1 A_SpawnItemEx("TeleportFog", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		TNT1 A 0 A_Jump(256,"Reward")
	Reward:
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("Stimpack", 0,0,2, 1,0,3, random(0,359), SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("CHCBasicArmor", 0,0,2, 0,0,3, 0, SXF_NOCHECKPOSITION)
		BEAC B 35
		BEAC B 2 A_FadeOut
		Wait
	}
}

ACTOR ArtiCarePackagePistol : ArtiCarePackageMedical
{
	Tag "Care Package Beacon (Pistolero)"
	Inventory.icon BEAC2ICN
	States
	{
	Use:
		TNT1 A 0 A_FireCustomMissile("CarePackageBeaconPistolThrown", 0, 0)
		Stop
	}
}


ACTOR CarePackageBeaconPistolThrown : CarePackageBeaconMedicalThrown
{
	States
	{
	Spawn:
		BEAC A -1
		Stop
	Death:
		TNT1 A 1 A_SpawnItemEx("CarePackageBeaconPistol", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR CarePackageBeaconPistol : CarePackageBeaconMedical
{
	States
	{
	Reward:
		TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("PistolAmmo", 0,0,2, 1,0,3, random(0,359), SXF_NOCHECKPOSITION)
		BEAC B 35
		BEAC B 2 A_FadeOut
		Wait
	}
}

ACTOR ArtiCarePackageShotgun : ArtiCarePackageMedical
{
	Tag "Care Package Beacon (Shotgun)"
	Inventory.icon BEAC3ICN
	States
	{
	Use:
		TNT1 A 0 A_FireCustomMissile("CarePackageBeaconShotgunThrown", 0, 0)
		Stop
	}
}


ACTOR CarePackageBeaconShotgunThrown : CarePackageBeaconMedicalThrown
{
	States
	{
	Spawn:
		BEAC A -1
		Stop
	Death:
		TNT1 A 1 A_SpawnItemEx("CarePackageBeaconShotgun", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR CarePackageBeaconShotgun : CarePackageBeaconMedical
{
	States
	{
	Reward:
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("ShotgunAmmo", 0,0,2, 1,0,3, random(0,359), SXF_NOCHECKPOSITION)
		BEAC B 35
		BEAC B 2 A_FadeOut
		Wait
	}
}

ACTOR ArtiCarePackageRifle : ArtiCarePackageMedical
{
	Tag "Care Package Beacon (Rifleman)"
	Inventory.icon BEAC4ICN
	States
	{
	Use:
		TNT1 A 0 A_FireCustomMissile("CarePackageBeaconRifleThrown", 0, 0)
		Stop
	}
}


ACTOR CarePackageBeaconRifleThrown : CarePackageBeaconMedicalThrown
{
	States
	{
	Spawn:
		BEAC A -1
		Stop
	Death:
		TNT1 A 1 A_SpawnItemEx("CarePackageBeaconRifle", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR CarePackageBeaconRifle : CarePackageBeaconMedical
{
	States
	{
	Reward:
		TNT1 AAAAAAAAAAAAA 0 A_SpawnItemEx("LightRifleAmmo", 0,0,2, 1,0,3, random(0,359), SXF_NOCHECKPOSITION)
		BEAC B 35
		BEAC B 2 A_FadeOut
		Wait
	}
}

/*
ACTOR IncendiaryGrenade // Let's use this later as a base for the MANIC Mines
{
	Radius 4
	Height 3
	Speed 40
	Scale 0.25
	Damage (1)
	DamageType "PlayerGrenadeFire"
	+NOBLOCKMAP
	+DROPOFF
	+MISSILE
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+CANBOUNCEWATER
	obituary "%o was roasted, toasted, and burnt to a crisp by %k."
	BounceType Doom
	BounceFactor 0.4
	WallBounceFactor 0.4
	bouncesound "grenade/bounce"
	states
	{
	Spawn:
		GCAN A 1 Bright A_Countdown
		GCAN BCDEFGH 1 Bright
		loop
	Death:
		GCAN D 35 Bright
		FBLX A 0 BRIGHT A_SetScale(1.0,1.0)
     	FBLX A 0 BRIGHT A_AlertMonsters
		FBLX A 0 BRIGHT A_SetTranslucent(0.75,1)
		FBLX A 0 BRIGHT A_PlaySound("grenade/explode",0,1.0,0,0.1)
		FBLX A 1 BRIGHT A_Explode(128,192,1)
		FBLX B 1 BRIGHT A_Quake(7,10,0,256,0)
		FBLX CCCCCCCCCC 0 BRIGHT A_CustomMissile("ThermaBurstMolotovChunk",0,0,(random(0,359)),6,(random(4,15)))
		FBLX CDEFGHIJK 2 BRIGHT
     	Stop
	}
}
*/